SYMBOLS {
  # the maximum size of a single line (also the size of the line buffer)
  __LINESIZE__:  type = export, value = 40;
}

MEMORY {
################################################################################
# ROM banks
  #-------------------------------------
  # ROM0
  # boot block, mapped to $a000 on boot
  ROM0:
    start = $a000
    size  = $2000
    fill  = yes
    bank  = 0;

  #-------------------------------------
  # ROM1
  # main "big" ROM area for Monster
  ROM1:
    start = $2000
    size  = $6000
    fill  = yes
    bank  = 1;

  ROM1_1:
    start = $a000
    size  = $2000
    fill  = yes
    bank  = 4;

  #-------------------------------------
  # ROMn
  # auxiliary "small" ROM banks.  These are banked in along with corresponding
  # RAM banks. They are banked in at address $a000 and each is given its
  # own RAM bank from $2000-$8000
  ROM2:
    start = $a000
    size  = $2000
    fill  = yes
    bank  = 5;
  ROM3:
    start = $a000
    size  = $2000
    fill  = yes
    bank  = 6;
  ROM4:
    start = $a000
    size  = $2000
    fill  = yes
    bank  = 7;
  ROM5:
    start = $a000
    size  = $2000
    fill  = yes
    bank  = 8;
  ROM6:
    start = $a000
    size  = $2000
    fill  = yes
    bank  = 9;
  ROM7:
    start = $a000
    size  = $2000
    fill  = yes
    bank  = 10;
  ROM8:
    start = $a000
    size  = $2000
    fill  = yes
    bank  = 11;
  ROM9:
    start = $a000
    size  = $2000
    fill  = yes
    bank  = 12;
  ROMA:
    start = $a000
    size  = $2000
    fill  = yes
    bank  = 13;

  ROMB:
    start = $a000
    size  = $2000
    fill  = yes
    bank  = 14;

  #-------------------------------------
  # BLOBn
  # ROM banks that are swapped into $a000 while the main ROM is active
  BLOB0:
    start = $a000
    size  = $2000
    fill  = yes
    bank  = 15;

  #-------------------------------------
  # ROMFTXT
  # Contains procedures for drawing text "fast"
  ROMFTXT:
    start = $2000
    size  = $6000
    fill  = yes
    bank  = 16;

################################################################################
# RAM banks
  #-------------------------------------
  # RAMn
  # These are banked in with their corresponding ROM banks
  RAM1:
    start = $a000
    size  = $2000
    bank  = 1;
  RAM2:
    start = $2000
    size  = $6000
    bank  = 2;
  RAM3:
    start = $2000
    size  = $6000
    bank  = 5;
  RAM4:
    start = $2000
    size  = $6000
    bank  = 8;
  RAM5:
    start = $2000
    size  = $6000
    bank  = 11;
  RAM6:
    start = $2000
    size  = $6000
    bank  = 14;
  RAM7:
    start = $2000
    size  = $6000
    bank  = 17;
  RAM8:
    start = $2000
    size  = $6000
    bank  = 20;
  RAM9:
    start = $2000
    size  = $6000
    bank  = 26;
  RAMA:
    start = $2000
    size  = $6000
    bank  = 29;
  SIMDBG:
    start = $6000
    size  = $1100
    bank  = 32;
  SIMDBG_00:
    start = $b100
    size  = $0f00
    bank  = 32;
  SIMBLK:
    start = $a000
    size  = $2000
    bank  = 33;
  SIM_REGION0:
    start = $2000
    size  = $2000
    bank  = 33;
  SIM_REGION_4:
    start = $4000
    size  = $5000	# start offset by $1000
    bank  = 37;		# IO region (virtual memory at $8000-$a000)

  #-------------------------------------
  # SHARE
  # shared RAM areas (these areas are not bank swapped)
  RAM123:
    start = $0400
    size  = $0c00
    bank  = 0;
  RAMIO:
    start = $9800
    size  = $0700
    bank  = 0;
  INTS:
    start = $9f00
    size  = $100-$11;
  ULTIREGS:
    # 1 byte for virtual "bank" register
    start = $9fef
    size  = $10+1
    bank  = 0;

  # Source is managed at a lower level than the usual bank interface
  # This MEMORY section defines the start/size for each source bank
  SOURCE:
    start = $2000
    size  = $6000
    bank  = 40;

  # Symbol names are also managed at a lower level
  # Names are stored in multiple banks in the Ultimem and banked into
  # BLK3 for searching/adding
  SYMBOLS:
    start = $6000
    size  = $2000;
}

SEGMENTS {
	#-----------------------------------------------------------------------
	# setup (boot block)
	CART:   load=ROM0, type=ro, define=yes;

	#-----------------------------------------------------------------------
	# common BSS segments
	SPARE:    load=RAM123,   type=bss;
	LINEBUFF: load=RAM123,   type=bss;
	SHAREBSS: load=RAM123,   type=bss;
	ULTIREGS: load=ULTIREGS, type=bss;

	#-----------------------------------------------------------------------
	# relocated code (lives in common RAM: RAM123, and IO space)
	SETUP:         load=ROM0,             type=ro, define=yes;
	IRQ:           load=ROM0, run=RAM123, type=rw, define=yes;
	BANKCODE:      load=ROM0, run=RAM123, type=rw, define=yes;
	BANKCODE2:     load=ROM0, run=RAM123, type=rw, define=yes;
	INTS:          load=ROM1, run=INTS,   type=rw, define=yes;
	STEP_EPILOGUE: load=ROM1,             type=ro;
	DATA:          load=ROM0, run=RAMIO,  type=rw, define=yes;
	ULTICFG:       load=ROM0, run=RAMIO,  type=rw, define=yes;

	#-----------------------------------------------------------------------
	# ROM1 (main code bank)
	DEBUGGER:   load=ROM1,   type=rw,  define=yes;
	CODE:       load=ROM1,   type=ro,  define=yes;
	RODATA:     load=ROM1_1, type=ro,  define=yes;
	BSS:        load=RAM123, type=bss, define=yes;
	BSS_NOINIT: load=RAM123, type=bss, define=yes;

	#-----------------------------------------------------------------------
	SOURCE:  load=SOURCE, type=bss;

	#-----------------------------------------------------------------------
	# ROM2
	DEBUGINFO_CODE: load=ROM2, type=ro,  define=yes;
	DEBUGINFO:      load=RAM2, type=bss;
	DEBUGINFO_VARS: load=RAM2, type=bss;
	DEBUGINFO_BSS:  load=RAM2, type=bss;

	#-----------------------------------------------------------------------
	# ROM3 (shared by UDGEDIT, HELP, and EXPR)
	UDGEDIT:  load=ROM3, type=ro,  define=yes;
	HELP:     load=ROM3, type=ro;
	EXPR:     load=ROM3, type=ro,  define=yes;
	EXPR_BSS: load=RAM3, type=bss;

	#-----------------------------------------------------------------------
	# ROM4
	MACROCODE:  load=ROM4, type=ro,  define=yes;
	MACRO_VARS: load=RAM4, type=bss;
	MACROBSS:   load=RAM4, type=bss;

	#-----------------------------------------------------------------------
	# ROM5
	LINKER:      load=ROM5, type=ro,  define=yes;
	LINKER_VARS: load=RAM5, type=bss, define=yes;
	LINKER_BSS:  load=RAM5, type=bss, define=yes;
	OBJCODE:     load=ROM5, type=ro,  define=yes;
	OBJVARS:     load=RAM5, type=bss, define=yes;
	OBJBSS:      load=RAM5, type=bss, define=yes;

	#-----------------------------------------------------------------------
	# ROM6
	COPYBUFF:      load=ROM6, type=ro,  define=yes;
	COPYBUFF_VARS: load=RAM6, type=bss, define=yes;
	COPYBUFF_BSS:  load=RAM6, type=bss, define=yes;

	#-----------------------------------------------------------------------
	# ROM7
	CONSOLE:      load=ROM7, type=ro;
	CONSOLE_BSS:  load=RAM7, type=bss;
	CONSOLE_VARS: load=RAM7, type=bss;

	#-----------------------------------------------------------------------
	# ROM8
	LABELS:     load=ROM8, type=ro;
	LABEL_BSS:  load=RAM8, type=bss;
	LABELMODES: load=RAM8, type=bss;
	LABELNAMES: load=SYMBOLS, type=bss;

	#-----------------------------------------------------------------------
	# ROM9
	CTX:     load=ROM9, type=ro;
	CTX_BSS: load=RAM9, type=bss;

	#-----------------------------------------------------------------------
	# BLOB storage (for some RODATA like error messages)
	ERRORS: load=BLOB0, type=ro;

	#-----------------------------------------------------------------------
	# ROM9
	VSCREEN:     load=ROMA, type=ro,  define=yes;
	VSCREEN_BSS: load=RAMA, type=bss, define=yes;

	#-----------------------------------------------------------------------
	# ROMA
	SIM:             load=ROMB,         type=ro;
	SIMDBG:          load=SIMDBG,       type=bss;
	SIMDBG_00:       load=SIMDBG_00,    type=bss;
	SIMRAM_INTERNAL: load=SIM_REGION0,  type=bss;
	SIMRAM_IO:       load=SIM_REGION0,  type=bss;

	#-----------------------------------------------------------------------
	# ROMFTXT
	FASTTEXT:     load=ROMFTXT, type=ro,  define=yes;
}
